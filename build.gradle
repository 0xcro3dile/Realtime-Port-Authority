// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        maven {
            url "http://download.osgeo.org/webdav/geotools/"
        }
        jcenter()
    }
}

// load secrets.properties for properties that should be part of app but not in source control
// maybe shouldn't be done in-line but somewhere else.
def secretProperties = new Properties()
def secretPropertiesFile = file('secrets.properties')
if (!secretPropertiesFile.exists()) {
    logger.error("App will not work as expected. Please copy-paste 'secrets.example.properties'" +
            " to a new file called 'secrets.properties' and add API keys to it")
} else {
    secretProperties.load(secretPropertiesFile.newReader())
}

subprojects {

    //region secret variables
    ext.kotlin_version = '1.0.6'
    /*
     * must define PAT_API_KEY in gradle.properties by adding pat_api=<KEY>
     */
    ext.patApiKey = "\"${secretProperties['pat_api']}\"" ?: '"Define Port Authority TrueTime API Key in secrets.properties#pat_api"';

    /*
     * must define Google Maps API Debug Key in gradle.properties by adding google_maps_api_debug=<KEY>
     * must have a project in Google Console Developers and add the correct key: https://console.developers.google.com
     * The SHA1 key from Android Studio's debug keystore. Find this in by the following in the command line:
     * `keytool -list -v -keystore ~/.android/debug.keystore -alias androiddebugkey -storepass android -keypass android`
     */
    ext.googleMapsApiKeyDebug = "\"${secretProperties['google_maps_api_debug']}\"" ?: '"Define Google Maps Debug API Key in secrets.properties#google_maps_api_debug"';

    /*
     * must define Google Maps API Release Key in gradle.properties by adding google_maps_api_release=<KEY>
     * must have a project in Google Console Developers and add the correct key: https://console.developers.google.com
     * This has to be generated from a signed keystore's SHA1 with similar instructions above
     */
    ext.googleMapsApiKeyRelease = "\"${secretProperties['google_maps_api_release']}\"" ?: '"Define Google Maps Release API Key in secrets.properties#google_maps_api_release"';
    // endregion

    // region versions for dependencies
    // perhaps we should actually create these as gradle properties? I'm thinking this is ok.

    // code language versions
    ext.javaVersion = JavaVersion.VERSION_1_8

    // google and android dependencies
    ext.androidMinSdkVersion = 16
    ext.androidTargetSdkVersion = 23
    ext.androidBuildVersion = '23.0.3'
    ext.googlePlayVersion = '10.0.1'
    ext.androidDependencyVersion = '23.4.0'

    // buildVersion for square dependencies and rxjava
    ext.rxJavaVersion = '1.2.1'
    ext.reactiveNetworkVersion = '0.6.0'
    ext.retrofitVersion = '2.1.0'
    ext.okHttpVersion = '3.4.1'
    ext.timberVersion = '4.3.1'
    ext.leakCanaryVersion = '1.5'

    // build versions for retrolambda
    ext.retrolambdaConfigVersion = '2.3.0'
    ext.retrolambdaPluginVersion = '3.3.1'

    // versions for other dependencies
    ext.javaAnnotationVersion = '10.0-b28'
    ext.gradleVersionsVersion = '0.12.0'
    ext.geotoolsVersion = '16.1'

    // versions for test dependencies
    ext.junitVersion = '4.12'
    ext.mockitoVersion = '1.9.5'
    // endregion

}
